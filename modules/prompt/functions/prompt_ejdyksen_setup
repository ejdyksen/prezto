function prompt_ejd_precmd {
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_ejd_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook

  add-zsh-hook precmd prompt_ejd_precmd

  zstyle ':prezto:module:editor' completing '%B%F{red}…%f%b'

  # You must use Xterm_256color_chart
  # color chart: http://upload.wikimedia.org/wikipedia/en/1/15/Xterm_256color_chart.svg
  zstyle ':prezto:module:git:info'            verbose 'yes'
  zstyle ':prezto:module:git:info:branch'     format '%b'             # %b
  zstyle ':prezto:module:git:info:ahead'      format '+%A'            # %A
  zstyle ':prezto:module:git:info:behind'     format '-%B'            # %B
  zstyle ':prezto:module:git:info:action'     format '%F{166}[%s] '   # %s
  zstyle ':prezto:module:git:info:commit'     format '[%7>>%c%>>]'    # %c
  zstyle ':prezto:module:git:info:clean'      format ' %F{118}✓'      # %C
  zstyle ':prezto:module:git:info:added'      format ' %F{010}%a'     # %a
  zstyle ':prezto:module:git:info:modified'   format ' %F{033}%m'     # %m
  zstyle ':prezto:module:git:info:renamed'    format ' %F{011}%r'     # %r
  zstyle ':prezto:module:git:info:untracked'  format ' %F{130}%u'     # %u
  zstyle ':prezto:module:git:info:deleted'    format ' %F{124}%d'     # %d
  zstyle ':prezto:module:git:info:dirty'      format 'dirty:%D'       # %D
  zstyle ':prezto:module:git:info:position'   format 'position:%p'    # %p <not used>
  zstyle ':prezto:module:git:info:remote'     format 'remote:%R'      # %R <not used>
  zstyle ':prezto:module:git:info:stashed'    format 'stashed:%S'     # %S <not used>
  zstyle ':prezto:module:git:info:unmerged'   format 'unmerged:%U'    # %U <not used>
  zstyle ':prezto:module:git:ignore' submodule ''
  zstyle ':prezto:module:git:info:keys' format \
  'prompt'  '%F{11}(%b%A%B)%f' \
  'rprompt' '%s%F{11}%c%C%a%m%r%d%u'

  PROMPT="%F{135}%n@%m%f: %F{002}%~%f "'${git_info[prompt]}'"
%(?.%F{002}.%F{124})❯ %f"

  RPROMPT='${git_info[rprompt]}'
}

prompt_ejd_setup "$@"

